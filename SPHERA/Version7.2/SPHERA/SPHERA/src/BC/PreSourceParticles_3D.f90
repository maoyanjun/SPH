!----------------------------------------------------------------------------------------------------------------------------------
! SPHERA v.8.0 (Smoothed Particle Hydrodynamics research software; mesh-less Computational Fluid Dynamics code).
! Copyright 2005-2015 (RSE SpA -formerly ERSE SpA, formerly CESI RICERCA, formerly CESI-Ricerca di Sistema)



! SPHERA authors and email contact are provided on SPHERA documentation.

! This file is part of SPHERA v.8.0.
! SPHERA v.8.0 is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
! SPHERA is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
! GNU General Public License for more details.
! You should have received a copy of the GNU General Public License
! along with SPHERA. If not, see <http://www.gnu.org/licenses/>.
!----------------------------------------------------------------------------------------------------------------------------------

!----------------------------------------------------------------------------------------------------------------------------------
! Program unit: PreSourceParticles_3D
! Description: To generate new source particles at the inlet section (only in 3D and with one quadrilateral inlet section).  
!----------------------------------------------------------------------------------------------------------------------------------

subroutine PreSourceParticles_3D 
!------------------------
! Modules
!------------------------ 
use Static_allocation_module
use I_O_file_module
use Hybrid_allocation_module
use Dynamic_allocation_module
!------------------------
! Declarations
!------------------------
implicit none
integer(4) :: nt,isi,sd,ip,i_source,i,j,NumPartR,NumPartS,nodes
double precision :: deltapart,eps,deltaR,deltaS,LenR,LenS,distR,distS,csi
double precision :: etalocal
!------------------------
! Explicit interfaces
!------------------------
!------------------------
! Allocations
!------------------------
!------------------------
! Initializations
!------------------------
! searching the ID of the source side
SourceFace = 0
SpCount = 0
i_source=0
!------------------------
! Statements
!------------------------
do isi=1,NumFacce
   nt = BoundaryFace(isi)%stretch
   if (Tratto(nt)%tipo=="sour") then
      SourceFace = isi
      i_source=i_source+1
      if ( SourceFace > 0 ) then
! It searches for the ID zone "partzone" to assign to the generated particles. 
! This index willl be the highest among those identifying particle volumes in 
! the field. Particle generated by the source will be automatically assigned 
! the medium ID related to the founded zone "partzone". In "partzone" is not 
! assigned (cases with only sources), then particles will inherit the
! ID zone from input. 
      nt = BoundaryFace(SourceFace)%stretch
      mat = Tratto(nt)%Medium
      izone = Tratto(nt)%zone
! Note: to insert a check in case of nodes=/4
      nodes = BoundaryFace(SourceFace)%nodes    
      deltapart = Domain%dd
! LenR and LenS are the length scales of the inlet section: they are computed
! as the distance between the first and the last inlet vertices and the third
! and the last inlet vertices, respectively. Particles are aligned with Plast-P1
! and Plast-P3, where P1 the first boundary vertex, ..., Plast the last boundary
! vertex. In case of a triangular inlet, we have particles aligned with one 
! direction: P3-P1. In case of a quadrilateral inlet, we have particles 
! distributed along two directions: P4-P1 and P4-P3.
      LenR = zero
      LenS = zero
      do sd=1,SPACEDIM
         LenR = LenR + (BoundaryFace(SourceFace)%Node(1)%GX(sd) -              &
                BoundaryFace(SourceFace)%Node(nodes)%GX(sd)) ** 2
         LenS = LenS + (BoundaryFace(SourceFace)%Node(3)%GX(sd) -              &
                BoundaryFace(SourceFace)%Node(nodes)%GX(sd)) ** 2
      end do
      LenR = Dsqrt(LenR)
      LenS = Dsqrt(LenS)
      NumPartR = Int(LenR / deltapart + 0.01d0)
      NumPartS = Int(LenS / deltapart + 0.01d0)
      deltaR = LenR / NumPartR 
      deltaS = LenS / NumPartS 
      eps = -half
      zfila = eps * deltapart
      distR = -half * deltaR
      ip = 0
      do i=1,NumPartR
         distR = distR + deltaR
         csi = distR / LenR
         distS = -half * deltaS
         do j=1,NumPartS
            distS = distS + deltaS
            etalocal = distS / LenS
            ip = ip + 1
            do sd=1,SPACEDIM
               P(sd) = BoundaryFace(SourceFace)%Node(4)%GX(sd) * (one - csi) + &
                       BoundaryFace(SourceFace)%Node(1)%GX(sd) * csi
               Q(sd) = BoundaryFace(SourceFace)%Node(3)%GX(sd) * (one - csi) + &
                       BoundaryFace(SourceFace)%Node(2)%GX(sd) * csi
               PartLine(i_source,ip,sd) = P(sd) * (one - etalocal) + Q(sd) *   &
                                          etalocal
            end do
         end do
      end do
      NumPartFace(i_source) = ip
      ParticleVolume = Domain%PVolume
      RowPeriod = ParticleVolume * NumPartFace(i_source) / Tratto(nt)%FlowRate 
      RowVelocity(i_source) = Domain%dd / RowPeriod
      Tratto(nt)%NormVelocity = RowVelocity(i_source)
      pinttimeratio = -1
   end if
end if 
end do 
!------------------------
! Deallocations
!------------------------
return
end subroutine PreSourceParticles_3D

